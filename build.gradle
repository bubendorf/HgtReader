plugins {
    id 'java'
}

group 'net.benpl'
version '1.1.1'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8
compileJava.options.encoding = 'UTF-8'

repositories {
    maven {
        url "http://maven.geomajas.org/"
    }
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation "javax.media:jai_core:1.1.3"
    implementation "javax.media:jai_codec:1.1.3"
    implementation "org.jaitools:jt-contour:1.5.0"
    implementation group: 'org.locationtech.jts', name: 'jts-core', version: '1.16.1'
    compileOnly "org.openstreetmap.osmosis:osmosis-core:0.47"
}

jar {
    from('src/main/config/') {
        filter {
            line -> line.replace('${hgtfile.reader.version}', archiveVersion.get())
        }
    }
}

task fatJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('jar-with-dependencies')
    from {
        (configurations.compileClasspath - configurations.compileOnly).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    with jar
}

artifacts {
    archives fatJar
}
